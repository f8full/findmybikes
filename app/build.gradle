apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 31
    packagingOptions {
        resources {
            excludes += ['META-INF/ASL2.0', 'META-INF/LICENSE', 'META-INF/NOTICE']
            pickFirsts += ['META-INF/atomicfu.kotlin_module']
        }
    }

    // workaround for "duplicate files during packaging of APK" issue
    // see https://groups.google.com/d/msg/adt-dev/bl5Rc4Szpzg/wC8cylTWuIEJ

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.ludoscity.findmybikes"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 999
        versionName "v2019.1 (99)"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // used by Room, to test migrations
        //https://medium.com/google-developers/7-steps-to-room-27a5fe5f99b2
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    // used by Room, to test migrations
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "app_version_name", variant.versionName
    }

    signingConfigs {
        debug {
            storeFile file("build_cloud/travis/debug.keystore")
        }

    }
    buildTypes {
        alpha {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "DATABASE_NAME", "\"findmybikes-database\""
        }
        beta {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "DATABASE_NAME", "\"findmybikes-database\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "DATABASE_NAME", "\"findmybikes-database\""
        }

        debug {
            minifyEnabled false
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            signingConfig signingConfigs.debug
            debuggable true
            buildConfigField "String", "DATABASE_NAME", "\"findmybikes-database-debug\""
        }
    }

    lint {
        abortOnError false
        checkReleaseBuilds true
        ignoreWarnings false
        quiet false
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    //implementation project(path: ':backend', configuration: 'android-endpoints')

    implementation project(':common')

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.gms:play-services-maps:16.1.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.libraries.places:places-compat:1.1.0'
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    implementation 'com.google.code.gson:gson:2.8.5'

    implementation 'androidx.room:room-runtime:2.4.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt 'androidx.room:room-compiler:2.4.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.0'

    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'

    implementation 'org.twitter4j:twitter4j-core:4.0.7'

    implementation 'com.gordonwong:material-sheet-fab:1.2.1'
    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'

    implementation 'de.psdev.licensesdialog:licensesdialog:2.0.0'

    implementation 'com.afollestad.material-dialogs:core:0.9.4.2'

    implementation 'com.dmitrymalkovich.android:progress-fab:1.6'
    implementation 'com.dinuscxj:circleprogressbar:1.1.1'
    implementation 'com.github.fondesa:kpermissions:1.0.0'

    debugImplementation 'com.amitshekhar.android:debug-db:1.0.1'

    androidTestImplementation 'androidx.room:room-testing:2.0.0'
    //androidTestImplementation 'androidx.annotation:annotation:1.1.0-beta01'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    // Optional -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:2.1'
    // Optional -- UI testing with Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    // Optional -- UI testing with UI Automator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

}
