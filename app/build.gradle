apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27

    // workaround for "duplicate files during packaging of APK" issue
    // see https://groups.google.com/d/msg/adt-dev/bl5Rc4Szpzg/wC8cylTWuIEJ
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.ludoscity.findmybikes"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 93
        versionName "v3.0 (93)"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // used by Room, to test migrations
        //https://medium.com/google-developers/7-steps-to-room-27a5fe5f99b2
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    // used by Room, to test migrations
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "app_version_name", variant.versionName
    }

    signingConfigs {
        debug {
            storeFile file("build_cloud/travis/debug.keystore")
        }

    }
    buildTypes {
        alpha {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        beta {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            signingConfig signingConfigs.debug
            debuggable true
        }
    }

    lintOptions {

        // set to true to turn off analysis progress reporting by lint
        quiet false
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings false

        // set to true to have all release builds run lint on issues with severity=fatal

        // and abort the build (controlled by abortOnError above) if fatal issues are found

        checkReleaseBuilds true
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    //implementation project(path: ':backend', configuration: 'android-endpoints')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v13:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:percent:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.google.android.gms:play-services-maps:16.1.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-places:16.0.0'
    implementation 'com.google.maps.android:android-maps-utils:0.4'
    implementation 'com.google.code.gson:gson:2.8.5'

    implementation "android.arch.persistence.room:runtime:1.1.1"
    implementation 'com.android.support:support-v4:27.1.1'
    kapt "android.arch.persistence.room:compiler:1.1.1"
    implementation "android.arch.lifecycle:extensions:1.1.1"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.1"

    implementation 'com.android.support:multidex:1.0.3'

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    implementation 'org.twitter4j:twitter4j-core:4.0.4'

    implementation 'com.gordonwong:material-sheet-fab:1.2.1'
    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'

    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'

    implementation 'com.afollestad.material-dialogs:core:0.9.4.2'

    implementation 'com.dmitrymalkovich.android:progress-fab:1.6'
    implementation 'com.dinuscxj:circleprogressbar:1.1.1'
    implementation 'com.github.fondesa:kpermissions:1.0.0'

    debugImplementation 'com.amitshekhar.android:debug-db:1.0.1'

    androidTestImplementation 'android.arch.persistence.room:testing:1.1.1'
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    // Optional -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    // Optional -- UI testing with Espresso
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    // Optional -- UI testing with UI Automator
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

}
