apply plugin: 'com.android.application'

android {
    compileSdkVersion 26

    // workaround for "duplicate files during packaging of APK" issue
    // see https://groups.google.com/d/msg/adt-dev/bl5Rc4Szpzg/wC8cylTWuIEJ
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    defaultConfig {
        applicationId "com.ludoscity.findmybikes"
        minSdkVersion 18
        targetSdkVersion 26
        versionCode 70
        versionName "v3.0alpha0"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // used by Room, to test migrations
        //https://medium.com/google-developers/7-steps-to-room-27a5fe5f99b2
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    // used by Room, to test migrations
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "app_version_name", variant.versionName
    }

    signingConfigs {
        debug {
            storeFile file("build_cloud/travis/debug.keystore")
        }

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            signingConfig signingConfigs.debug
        }
    }

    lintOptions {

        // set to true to turn off analysis progress reporting by lint
        quiet false
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings false

        // set to true to have all release builds run lint on issues with severity=fatal

        // and abort the build (controlled by abortOnError above) if fatal issues are found

        checkReleaseBuilds true
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation project(path: ':backend', configuration: 'android-endpoints')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:support-v13:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:percent:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.google.android.gms:play-services-maps:9.8.0'
    implementation 'com.google.android.gms:play-services-location:9.8.0'
    implementation 'com.google.android.gms:play-services-places:9.8.0'
    implementation 'com.google.maps.android:android-maps-utils:0.4'
    implementation 'com.google.code.gson:gson:2.8.0'

    implementation "android.arch.persistence.room:runtime:1.0.0"
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0"
    implementation "android.arch.lifecycle:extensions:1.0.0"

    implementation 'com.android.support:multidex:1.0.2'

    implementation 'com.squareup.retrofit2:retrofit:2.2.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.0.2'

    implementation 'org.twitter4j:twitter4j-core:4.0.4'

    implementation 'com.gordonwong:material-sheet-fab:1.2.1'
    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'

    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.0'

    implementation 'com.afollestad.material-dialogs:core:0.9.4.2'

    implementation 'com.dmitrymalkovich.android:progress-fab:1.6'
    implementation 'com.dinuscxj:circleprogressbar:1.1.1'

    debugImplementation 'com.amitshekhar.android:debug-db:1.0.1'

    androidTestImplementation 'android.arch.persistence.room:testing:1.0.0'
    androidTestImplementation 'com.android.support:support-annotations:26.1.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    // Optional -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    // Optional -- UI testing with Espresso
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    // Optional -- UI testing with UI Automator
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'

}